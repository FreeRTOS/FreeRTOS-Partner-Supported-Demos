# Copyright 2021-2024 Arm Limited and/or its affiliates
# <open-source-office@arm.com>
# SPDX-License-Identifier: MIT

# Files generated by TF-M build must be listed as BUILD_BYPRODUCTS
# to inform CMake that they don't exist before build starts. Include
# paths do not need to be listed.
# <BINARY_DIR> is a placeholder keyword in ExternalProject_Add.

set(tfm_ns_interface_generated
    <BINARY_DIR>/api_ns/interface/src/tfm_tz_psa_ns_api.c
    <BINARY_DIR>/api_ns/interface/src/tfm_ps_api.c
    <BINARY_DIR>/api_ns/interface/src/tfm_its_api.c
    <BINARY_DIR>/api_ns/interface/src/tfm_crypto_api.c
    <BINARY_DIR>/api_ns/interface/src/tfm_attest_api.c
    <BINARY_DIR>/api_ns/interface/src/tfm_platform_api.c
    <BINARY_DIR>/api_ns/interface/src/os_wrapper/tfm_ns_interface_rtos.c
)

set(s_veneers_generated
    <BINARY_DIR>/api_ns/interface/lib/s_veneers.o
)

include(ExternalProject)

if(CMAKE_C_COMPILER_ID STREQUAL "ARMClang")
    set(tfm_toolchain_file "toolchain_ARMCLANG.cmake")
elseif(CMAKE_C_COMPILER_ID STREQUAL "IAR")
    set(tfm_toolchain_file "toolchain_IARARM.cmake")
else()
    message(FATAL_ERROR "Unsupported compiler: ${CMAKE_C_COMPILER_ID}")
endif()

ExternalProject_Add(
    trusted_firmware-m-build

    DOWNLOAD_COMMAND    ""
    SOURCE_DIR          ${trusted_firmware-m_SOURCE_DIR}

    USES_TERMINAL_CONFIGURE ON
    USES_TERMINAL_BUILD     ON

    BUILD_ALWAYS ON

    CMAKE_ARGS
        -DTFM_TOOLCHAIN_FILE=<SOURCE_DIR>/${tfm_toolchain_file}
        -DCMAKE_BUILD_TYPE=Release
        -DCONFIG_TFM_ENABLE_CP10CP11=ON
        -DMCUBOOT_GENERATE_SIGNING_KEYPAIR=ON
        -DMCUBOOT_LOG_LEVEL=INFO
        -DMCUBOOT_SIGNATURE_KEY_LEN=3072
        -DNS=ON
        -DPLATFORM_DEFAULT_PROVISIONING=OFF
        -DPLATFORM_DEFAULT_UART_STDOUT=ON
        -DTFM_DUMMY_PROVISIONING=OFF
        -DTFM_EXCEPTION_INFO_DUMP=ON
        -DTFM_PARTITION_CRYPTO=ON
        -DTFM_PARTITION_INITIAL_ATTESTATION=ON
        -DTFM_PARTITION_INTERNAL_TRUSTED_STORAGE=ON
        -DTFM_PARTITION_PLATFORM=ON
        -DTFM_PARTITION_PROTECTED_STORAGE=ON
        -DTFM_SPM_LOG_LEVEL=TFM_SPM_LOG_LEVEL_INFO
        -DTFM_PLATFORM=arm/mps4/corstone315
        -DFLASH_S_PARTITION_SIZE=0x40000
        -DFLASH_NS_PARTITION_SIZE=0x340000
        -DPROJECT_CONFIG_HEADER_FILE=${CMAKE_CURRENT_LIST_DIR}/../../../config/project_config.h
        -DCONFIG_TFM_BRANCH_PROTECTION_FEAT=${TFM_PACBTI_CONFIGURATION}

    PATCH_COMMAND
        ${TFM_PATCH_COMMAND}

    BUILD_BYPRODUCTS
        ${tfm_ns_interface_generated}
        ${s_veneers_generated}
)

# The path ${BINARY_DIR} is available after ExternalProject_Add.
# Convert <BINARY_DIR> to allow projects to use those files.
ExternalProject_Get_Property(trusted_firmware-m-build BINARY_DIR)
list(TRANSFORM tfm_ns_interface_generated REPLACE "<BINARY_DIR>" "${BINARY_DIR}")
list(TRANSFORM s_veneers_generated REPLACE "<BINARY_DIR>" "${BINARY_DIR}")
